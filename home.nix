{
  config,
  inputs,
  pkgs,
  hostMain,
  user,
  ...
}: let
  dotfiles = "${config.home.homeDirectory}/UbuntuConfig/dotfiles";
  create_symlink = path: config.lib.file.mkOutOfStoreSymlink path;

  # Tạo danh sách các ứng dụng cần tạo symlink từ thư mục dotfiles
  configApps = ["foot" "weathr" "vicinae" "fastfetch" "btop" "bat" "DankMaterialShell" "nvim" "niri" "kitty" "starship" "yazi"];
in {
  imports = [
    inputs.catppuccin.homeModules.catppuccin
    ./modules
  ];

  catppuccin.autoEnable = true;

  home.username = "${user}";
  home.homeDirectory = "/home/${user}";
  home.stateVersion = hostMain.stateVersion;
  targets.genericLinux.enable = true;

  # Tối ưu hóa toàn bộ phần định nghĩa xdg.configFile cũ bằng một vòng lặp map
  xdg.configFile = builtins.listToAttrs (map (app: {
      name = app;
      value = {
        source = create_symlink "${dotfiles}/${app}/";
        recursive = true;
      };
    })
    configApps);

  home.packages = with pkgs; [
    nerd-fonts.fira-code
    nerd-fonts.jetbrains-mono
    wl-clipboard
    vicinae
    fzf
    tree
    bat
    git
    fuzzel
    fastfetch
    swaybg
    neovim
    alejandra
    ripgrep
    nil
    nixpkgs-fmt
    nodejs
    btop
    gcc
    yazi
    starship
    kitty
    foot
    kew
    ddcutil # brightness
    eza # alterlative ls
  ];
}
